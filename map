#!/usr/local/bin/gawk -f

# Treemap: Web application for mobiles that displays information on 
#   georeferenced trees over a base map
# Copyright (c) 2011, Campbell Webb <cwebb@oeb.harvard.edu>
# Free/Open Source Software using BSD 2-clause license
# Requires gawk v4. See http://www.gnu.org/software/gawk/

# Include function libraries
@include "lib/colors.awk" ;
@include "lib/html.awk" ;
@include "lib/mapHi.awk" ;
@include "lib/mapLo.awk" ;
@include "lib/utils.awk" ;
@include "lib/info.awk" ;
@include "lib/search.awk" ;
@include "lib/obs.awk" ;
@include "lib/docs.awk" ;

BEGIN{

  # INITIALIZATION
  # unique process code
  srand();
  ps = substr(rand(),3);

  # clean old files (older than one minute)
  system("rm -f `find run -type f -cmin +1 -not -name .gitignore -print` \
          >& /dev/null");

  # READ QUERY STRING
  split(ENVIRON["QUERY_STRING"], query, "&");
  for (i1 in query)
	{
	  split(query[i1], tmpq, "=");
	  f[tmpq[1]] = urldecode(tmpq[2]); 
   }

  # DETERMINE AND EXECUTE METHOD
  # NB: variables in these functions  are not protected; care not to 
  #     introduce new variable in this main loop with clashing names 
  if      (f["method"] == "mapHi")  mapHi() ;
  else if (f["method"] == "mapLo")  mapLo() ;
  else if (f["method"] == "info")   treeInfo() ;
  else if (f["method"] == "search") search() ;
  else if (f["method"] == "about")  about() ;
  else if (f["method"] == "obs")    obsForm() ;
  else if (f["method"] == "obsD")   obsCapture() ;
  else if (f["method"] == "obsS")   obsReport() ;
  else if (f["method"] == "colorkey")   colorkey() ;
  else if (f["method"] == "latinlist")   latinlist() ;
  
  # other/no arguments
  else mapLo() ;
  
  # RETURN FOOTER
  htmlFooter();

  exit ;
}


